import com.github.jk1.license.render.*

plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.springframework.boot' version '2.2.5.RELEASE' apply false
    id 'fr.brouillard.oss.gradle.jgitver' version '0.9.1'
    id 'io.freefair.lombok' version '4.1.6'
    id 'org.owasp.dependencycheck' version '5.3.0'
    id 'com.github.jk1.dependency-license-report' version '1.12'
}

allprojects {
    apply plugin: 'idea'
}

licenseReport {
    renderers = [new InventoryHtmlReportRenderer()]
}

subprojects {
    group = 'everest.engineering.starter.kit'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'io.freefair.lombok'

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        mavenLocal()
        jcenter()
        maven {
            url  "https://dl.bintray.com/everestengineering/starterkit"
        }
    }

    ext {
        axonHazelcastCommandDistributionVersion = '0.0.9'
        mediaVersion = '0.0.11'
        securityVersion = '0.0.8'
        storageVersion = '0.0.11'

        aspectjVersion = '1.9.5'
        awsSdkVersion = '1.11.740'
        axonVersion = '4.2'
        bouncyCastleVersion = '1.64'
        commonsLangVersion = '3.9'
        commonsTextVersion = '1.8'
        commonsValidatorVersion = '1.6'
        ehCacheVersion = '3.8.1'
        guavaVersion = '28.1-jre'
        hazelcastKubernetesVersion = '1.5.2'
        hazelcastVersion = '3.11.7'
        javaXCacheVersion = '1.1.1'
        javaXPersistenceVersion = '2.2'
        jjwtVersion = '0.11.0'
        jodaTimeVersion = '2.10.4'
        kubernetesClientVersion = '6.0.1'
        liquibaseVersion = '3.8.7'
        microMeterPrometheusRegistry = '1.3.1'
        postgresDriverVersion = '42.2.8'
        prometheusPushGateway = '0.8.0'
        swaggerVersion = '2.9.2'
        thumbnailatorVersion = '0.4.11'
        springBootVersion = '2.2.5.RELEASE'

        h2Version = '1.4.200'
        hamcrestVersion = '2.2'
        mockitoVersion = '3.2.4'
        junitVersion = '5.6.0'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    lombok {
        version = "1.18.12"
        generateLombokConfig.enabled = false
    }

    checkstyle {
        toolVersion = '8.24'
        maxWarnings = 0
        configFile = file("${project.rootDir}/build-config/checkstyle/checkstyle.xml")
        sourceSets = [sourceSets.main]
    }

    pmd {
        toolVersion = '6.17.0'
        incrementalAnalysis = true
        ruleSets = []
        ruleSetFiles = files("${project.rootDir}/build-config/pmd/pmd-ruleset.xml")
        sourceSets = [sourceSets.main]
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "skipped", "failed"
        }
    }

    tasks.withType(JavaCompile) {
        options.deprecation = true
    }
}
